(ns comfyui.workflow-test
  (:require [clojure.test :refer [deftest is testing]]
            [comfyui.node :as node]
            [comfyui.std :as std]
            [comfyui.workflow :as wf]))

(defn- reg [] (node/registry std/all))

(def wf-sum
  {"1" {:class_type "PrimitiveInt" :inputs {:value 2}}
   "2" {:class_type "PrimitiveInt" :inputs {:value 3}}
   "3" {:class_type "Add" :inputs {:a ["1" 0] :b ["2" 0]}}})

(deftest topo-and-deps
  (is (= ["1" "2" "3"] (wf/topo-sort wf-sum)))
  (is (= ["1" "2"] (sort (wf/dependencies wf-sum "3"))))
  (is (= #{"1" "2" "3"} (wf/ancestors-of wf-sum "3")))
  (is (= #{"1"} (wf/ancestors-of wf-sum "1")))
  (testing "cycle detection"
    (is (thrown? #?(:clj Exception :cljs js/Error)
                 (wf/topo-sort {"a" {:class_type "Add" :inputs {:a ["b" 0]}}
                                "b" {:class_type "Add" :inputs {:a ["a" 0]}}})))))

(deftest validation
  (testing "valid workflow"
    (is (:valid? (wf/validate (reg) wf-sum))))
  (testing "unknown class"
    (is (= :unknown-class
           (-> (wf/validate (reg) {"1" {:class_type "Nope" :inputs {}}})
               :errors first :error))))
  (testing "missing required input"
    (is (= :missing-input
           (-> (wf/validate (reg) {"1" {:class_type "UpperCase" :inputs {}}})
               :errors first :error))))
  (testing "type mismatch on a link"
    (let [w {"1" {:class_type "PrimitiveString" :inputs {:value "x"}}
             "2" {:class_type "Add" :inputs {:a ["1" 0] :b 1}}}]
      (is (= :type-mismatch
             (-> (wf/validate (reg) w) :errors first :error)))))
  (testing "dangling link and bad constant"
    (let [w {"2" {:class_type "Add" :inputs {:a ["9" 0] :b "one"}}}
          errs (set (map :error (:errors (wf/validate (reg) w))))]
      (is (contains? errs :dangling-link))
      (is (contains? errs :bad-value))))
  (testing "no-such-output"
    (let [w {"1" {:class_type "PrimitiveInt" :inputs {:value 1}}
             "2" {:class_type "Add" :inputs {:a ["1" 5] :b 1}}}]
      (is (= :no-such-output
             (-> (wf/validate (reg) w) :errors first :error))))))

(deftest registry-introspection
  (let [r (reg)]
    (is (contains? (node/object-info r) "Add"))
    (is (nil? (get-in (node/object-info r) ["Add" :fn])))
    (is (some #{"math"} (node/categories r)))
    (is (= ["1"] (wf/output-nodes r {"1" {:class_type "Preview" :inputs {:value 1}}
                                     "2" {:class_type "Add" :inputs {}}})))))
