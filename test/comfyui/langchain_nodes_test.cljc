(ns comfyui.langchain-nodes-test
  (:require [clojure.test :refer [deftest is testing]]
            [comfyui.node :as node]
            [comfyui.std :as std]
            [comfyui.exec :as exec]
            [comfyui.viz :as viz]
            [comfyui.nodes.langchain :as lc-nodes]
            [langchain.model :as model]
            [langchain.message :as msg]))

(def weather-tool
  {:name "get_weather"
   :description "Get current weather for a location"
   :schema {:type "object"
            :properties {:location {:type "string"}}
            :required ["location"]}
   :fn (fn [{:keys [location]}] (str "72F and sunny in " location))})

(deftest llm-pipeline
  (let [mock (model/mock-model
              (fn [messages _]
                (msg/ai (str "LLM<" (:content (msg/last-message messages)) ">"))))
        reg (-> (node/registry std/all)
                (node/register! (lc-nodes/pack mock [weather-tool])))
        ;; weather → format prompt → chat → upper-case
        w {"1" {:class_type "PrimitiveString" :inputs {:value "Paris"}}
           "2" {:class_type "get_weather" :inputs {:location ["1" 0]}}
           "3" {:class_type "FormatText"
                :inputs {:template "Summarize: {a}" :a ["2" 0]}}
           "4" {:class_type "ChatModel" :inputs {:prompt ["3" 0]}}
           "5" {:class_type "UpperCase" :inputs {:text ["4" 0]}}}
        {:keys [results]} (exec/execute {:registry reg} w)]
    (is (= ["72F and sunny in Paris"] (get results "2")))
    (is (= ["LLM<Summarize: 72F and sunny in Paris>"] (get results "4")))
    (is (= ["LLM<SUMMARIZE: 72F AND SUNNY IN PARIS>"] (get results "5")))
    (testing "mermaid rendering"
      (let [mm (viz/mermaid w)]
        (is (re-find #"flowchart TD" mm))
        (is (re-find #"ChatModel" mm))
        (is (re-find #"n3 -->" mm))))))

(deftest tool-node-error-propagates
  (let [boom {:name "boom" :description ""
              :schema {:type "object" :properties {}}
              :fn (fn [_] (throw (ex-info "kaboom" {})))}
        reg (-> (node/registry std/all)
                (node/register! (lc-nodes/tool-node boom)))]
    (is (thrown? #?(:clj Exception :cljs js/Error)
                 (exec/execute {:registry reg}
                               {"1" {:class_type "boom" :inputs {}}})))))
